apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'witness'
apply from: 'witness-verifications.gradle'

android {
    buildToolsVersion BUILD_TOOL_VERSION
    compileSdkVersion COMPILE_SDK

    defaultConfig {
        minSdkVersion MINIMUM_SDK
        targetSdkVersion TARGET_SDK
        multiDexEnabled true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JAVA_VERSION
        targetCompatibility JAVA_VERSION
    }
}

dependencyVerification {
    configuration = '(debug|release)RuntimeClasspath'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.10.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    lintChecks project(':lintchecks')

    coreLibraryDesugaring libs.android.tools.desugar

    api libs.androidx.annotation

    implementation libs.google.protobuf.javalite

    testImplementation testLibs.junit.junit
    testImplementation testLibs.mockito.core
}
